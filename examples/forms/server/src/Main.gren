module Main exposing (main)

import Node exposing (Environment)
import Prettynice.FormData as FormData exposing (FormData)
import Prettynice.SimpleRouter as Router
import Prettynice.Request exposing (Request)
import Prettynice.Response as Response exposing (Response)
import HttpServer exposing (Method(..))
import Transmutable.Html as H exposing (Html)
import Transmutable.Html.Attributes as A


main : Router.Program
main =
    Router.defineProgram
        { init = init
        , router = router
        }


init : Environment -> Router.Init
init env =
    Router.startProgram
        { host = "0.0.0.0"
        , port_ = 3000
        , env = env
        }


router : Request -> Response -> Cmd msg
router request response =
    case request of
        { method = POST, path = [ "submission" ] } ->
            response
                |> Response.sendHtml
                    { title = "Form Submitted"
                    , head = []
                    , body = [ viewResult request.formData ]
                    }
        
        _ -> 
            response
                |> Response.sendHtml
                    { title = "Form example"
                    , head = []
                    , body = [ viewForm ]
                    }


viewResult : FormData -> Html msg
viewResult form =
    let
        name =
            FormData.get "name" form
                |> Maybe.withDefault "Mr. E"

        hobbies =
            FormData.getAll "hobbies" form
                |> String.join ", "
    in
    H.text <|
        name ++ " likes: " ++ hobbies


viewForm : Html msg
viewForm =
    H.form 
        [ A.method "post", A.action "/submission" ]
        [ H.div []
            [ H.label [] 
                [ H.text "Name: " 
                , H.input [ A.type_ "text", A.name "name" ] []
                ]
            ]
        , H.div []
            [ H.text "Hobbies: "
            , H.label []
                [ H.text "Clocks"
                , checkbox "hobbies" "clocks"
                ]
            , H.label []
                [ H.text "Empathy"
                , checkbox "hobbies" "empathy"
                ]
            , H.label []
                [ H.text "Bugs"
                , checkbox "hobbies" "bugs"
                ]
            ]
        , H.div []
            [ H.input [ A.type_ "submit", A.value "Submit" ] []
            ]
        ]


checkbox : String -> String -> Html msg
checkbox name value =
    H.input
        [ A.type_ "checkbox"
        , A.name name
        , A.value value
        ]
        []
