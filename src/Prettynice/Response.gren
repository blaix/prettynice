module Prettynice.Response exposing 
    ( Response
    , Body(..)
    , new
    , send
    , sendText
    , sendHtml
    , sendJson
    , sendBytes
    , setBody
    , setHeader
    , setStatus
    ) 


{-| HTTP Responses

@docs Response, Body, new

## Updating Responses

@docs setStatus, setHeader, setBody

## Sending Responses

You can construct and send a [`Response`](#Response) directly with [`send`](#send),
or you can use one of the `send*` helpers to send data with the content-type set automatically.

@docs send, sendHtml, sendText, sendJson, sendBytes

-}

import Bytes exposing (Bytes)
import HttpServer.Response as Http
import Prettynice.Request exposing (Request)
import Json.Encode
import Task exposing (Task)
import Transmutable.Html as H exposing (Html)
import Transmutable.Html.Attributes as A


{-| An HTTP response value.
-}
type Response
    = Response Http.Response


{-| Custom type for text-based response bodies.
-}
type Body msg
    = HtmlBody 
        { title : String
        , head : Array (Html msg)
        , body : Array (Html msg)
        }
    | JsonBody Json.Encode.Value
    | TextBody String


{-| Turn a [Node HttpServer Response](https://packages.gren-lang.org/package/gren-lang/node/version/3.1.0/module/HttpServer.Response#Response)
into a Prettynice Response.

You shouldn't need this in your normal application code.
The response you get in a Prettynice request message is already a Prettynice Response.
-}
new : Http.Response -> Response
new httpResponse =
    Response httpResponse


{-| Command to send a response to the current HTTP request.
-}
send : Response -> Cmd a
send response =
    response
        |> getHttpResponse
        |> Http.send


{-| Respond with raw bytes.

Use this to send binary data like files, images, etc.
-}
sendBytes : { contentType : String, body : Bytes } -> Response -> Cmd a
sendBytes { contentType, body } response =
    response
        |> getHttpResponse
        |> Http.setHeader "Content-type" contentType
        |> Http.setBodyAsBytes body
        |> Http.send


{-| Respond with plain text.
-}
sendText : String -> Response -> Cmd a
sendText text response =
    response
        |> setBody (TextBody text)
        |> send


{-| Respond with Json.
-}
sendJson : Json.Encode.Value -> Response -> Cmd a
sendJson json response =
    response
        |> setBody (JsonBody json)
        |> send


{-| Respond with HTML.

`title` will go into the `<title>` tag,
`head` will be added to the `<head>` tag,
and `body` will go into the `<body>` tag.

Note that Prettynice uses and expects Html from [icidasset/html-gren](https://packages.gren-lang.org/package/icidasset/html-gren),
not from gren-lang/browser. The API is the same, but html-gren is needed because it supports server-side rendering.
-}
sendHtml : 
    { title : String, head : Array (Html msg), body : Array (Html msg) } 
    -> Response 
    -> Cmd msg
sendHtml { title, head, body } response =
    response
        |> setBody (HtmlBody { title = title, head = head, body = body })
        |> send


{-| Set the body of the response directly.

Normally you can use one of the `send*` functions,
but if you need more control, you can set a [`Body`](#Body) directly.
-}
setBody : Body msg -> Response -> Response
setBody bodyVariant response =
    let
        { contentType, rawBody } =
            case bodyVariant of
                HtmlBody { title, head, body } ->
                    { contentType = "text/html"
                    , rawBody =
                        ( H.toString H.doctype ) ++ 
                        ( H.toString
                            ( H.html [ A.lang "en" ]
                                [ H.head []
                                    ( [ H.meta [ A.charset "utf-8" ]
                                      , H.meta [ A.name "viewport", A.content "width=device-width, initial-scale=1.0" ]
                                      , H.node "script" [ A.src "/main.js" ] []
                                      , H.title [ H.text title ]
                                      ] ++ head
                                    )
                                , H.body [] <|
                                    body ++
                                    [ H.node "script" []
                                        [ H.text <|
                                            """
                                            if (typeof($__components) !== 'undefined') {
                                                Object.keys($__components).map((name) => {
                                                    let path = name.replace(/\\./g, '/');
                                                    import("/" + path + ".js").then((mod) => {
                                                        if (mod.init) {
                                                            $__components[name].map((comp) => {
                                                                mod.init(comp);
                                                            });
                                                        }
                                                    });
                                                });
                                            }
                                            """
                                        ]
                                    ]
                                ]
                            )
                        )
                    }

                JsonBody json ->
                    { contentType = "application/json"
                    , rawBody = Json.Encode.encode 2 json
                    }

                TextBody text ->
                    { contentType = "text/plain"
                    , rawBody = text
                    }

    in
    response
        |> getHttpResponse
        |> Http.setHeader "Content-type" contentType
        |> Http.setBody rawBody
        |> new


{-| Add a header to the response.
-}
setHeader : String -> String -> Response -> Response
setHeader key val (Response httpResponse) =
    httpResponse
        |> Http.setHeader key val
        |> Response
    

{-| Set the status of the response.
-}
setStatus : Int -> Response -> Response
setStatus status (Response httpResponse) =
    httpResponse
        |> Http.setStatus status
        |> Response


getHttpResponse : Response -> Http.Response
getHttpResponse (Response httpResponse) =
    httpResponse
