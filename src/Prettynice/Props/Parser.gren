module Prettynice.Props.Parser exposing 
    ( ParsedType
    , TypeDefinition(..)
    , RecordField
    , CustomVariant
    , FieldType(..)
    , parseTypeDefinitions
    , findPropsType
    )

import Parser exposing (..)
import Parser exposing (Step(..))
import Set


type alias ParsedType =
    { name : String
    , definition : TypeDefinition
    }


type TypeDefinition
    = RecordAlias (Array RecordField)
    | CustomType (Array CustomVariant)


type alias RecordField =
    { name : String
    , fieldType : FieldType
    }


type alias CustomVariant =
    { name : String
    , argument : Maybe FieldType
    }


type FieldType
    = StringType
    | IntType
    | FloatType
    | BoolType
    | ArrayType FieldType
    | CustomFieldType String
    | RecordType (Array RecordField)


parseTypeDefinitions : String -> Result (Array DeadEnd) (Array ParsedType)
parseTypeDefinitions source =
    run manyTypeDefinitions source


manyTypeDefinitions : Parser (Array ParsedType)
manyTypeDefinitions =
    succeed identity
        |. spaces
        |= parseTypeDefinitionsRecursive []


parseTypeDefinitionsRecursive : Array ParsedType -> Parser (Array ParsedType)  
parseTypeDefinitionsRecursive acc =
    oneOf
        [ succeed (\def -> parseTypeDefinitionsRecursive (Array.pushLast def acc))
            |. spaces
            |= typeDefinitionParser
            |> andThen identity
        , succeed acc
        ]


typeDefinitionParser : Parser ParsedType
typeDefinitionParser =
    oneOf
        [ recordAliasParser |> backtrackable
        , customTypeDefinitionParser
        ]


recordAliasParser : Parser ParsedType
recordAliasParser =
    succeed (\name fields -> { name = name, definition = RecordAlias fields })
        |. spaces
        |. keyword "type"
        |. spaces
        |. keyword "alias"
        |. spaces
        |= variable { start = Char.isAlpha, inner = \c -> Char.isAlphaNum c || c == '_', reserved = Set.empty }
        |. spaces
        |. symbol "="
        |. spaces
        |= recordFieldsParser


customTypeDefinitionParser : Parser ParsedType
customTypeDefinitionParser =
    succeed (\name variants -> { name = name, definition = CustomType variants })
        |. spaces
        |. keyword "type"
        |. spaces
        |= variable { start = Char.isAlpha, inner = \c -> Char.isAlphaNum c || c == '_', reserved = Set.empty }
        |. spaces
        |. symbol "="
        |. spaces
        |= customTypeVariantsParser


recordFieldsParser : Parser (Array RecordField)
recordFieldsParser =
    succeed identity
        |. symbol "{"
        |. spaces
        |= sequence
            { start = ""
            , separator = ","
            , end = "}"
            , spaces = spaces
            , item = recordFieldParser
            , trailing = Optional
            }


recordFieldParser : Parser RecordField
recordFieldParser =
    succeed (\name fieldType -> { name = name, fieldType = fieldType })
        |= variable { start = Char.isAlpha, inner = \c -> Char.isAlphaNum c || c == '_', reserved = Set.empty }
        |. spaces
        |. symbol ":"
        |. spaces
        |= fieldTypeParser


fieldTypeParser : Parser FieldType
fieldTypeParser =
    lazy (\_ -> fieldTypeParserHelper)


fieldTypeParserHelper : Parser FieldType
fieldTypeParserHelper =
    oneOf
        [ succeed StringType |. keyword "String"
        , succeed IntType |. keyword "Int"
        , succeed FloatType |. keyword "Float"
        , succeed BoolType |. keyword "Bool"
        , succeed ArrayType
            |. keyword "Array"
            |. spaces
            |= fieldTypeParser
        , succeed (\fields -> RecordType fields)
            |. symbol "{"
            |. spaces
            |= sequence
                { start = ""
                , separator = ","
                , end = "}"
                , spaces = spaces
                , item = recordFieldParser
                , trailing = Optional
                }
        , succeed CustomFieldType
            |= variable { start = Char.isAlpha, inner = \c -> Char.isAlphaNum c || c == '_', reserved = Set.empty }
        ]


customTypeVariantsParser : Parser (Array CustomVariant)
customTypeVariantsParser =
    succeed (\first rest -> Array.pushFirst first rest)
        |. spaces
        |= customVariantParser
        |= parseRestVariants []


parseRestVariants : Array CustomVariant -> Parser (Array CustomVariant)
parseRestVariants acc =
    oneOf
        [ succeed (\variant -> parseRestVariants (Array.pushLast variant acc))
            |. chompWhile (\c -> c == ' ' || c == '\t')
            |. symbol "|"
            |. spaces
            |= customVariantParser
            |> andThen identity
        , succeed acc
        ]


customVariantParser : Parser CustomVariant
customVariantParser =
    succeed (\name argument -> { name = name, argument = argument })
        |= variable { start = Char.isAlpha, inner = \c -> Char.isAlphaNum c || c == '_', reserved = Set.empty }
        |= parseOptionalArgument


parseOptionalArgument : Parser (Maybe FieldType)
parseOptionalArgument =
    oneOf
        [ succeed Just
            |. chompIf (\c -> c == ' ' || c == '\t')
            |. chompWhile (\c -> c == ' ' || c == '\t')
            |= fieldTypeParser
            |> backtrackable
        , succeed Nothing
        ]


findPropsType : Array ParsedType -> Maybe ParsedType
findPropsType typeDefs =
    Array.findFirst (\def -> def.name == "Props") typeDefs
        |> Maybe.map .value
